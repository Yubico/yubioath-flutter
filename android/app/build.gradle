plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'dev.flutter.flutter-gradle-plugin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.diffplug.spotless'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {

    namespace = 'com.yubico.authenticator'

    compileSdkVersion project.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.yubico.yubioath"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    buildFeatures {
        buildConfig = true
    }

    lint {
        abortOnError = false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86: 3, x86_64: 4]
            def abiCode = abiCodes.get(output.getFilter(com.android.build.OutputFile.ABI))
            output.versionCodeOverride = variant.versionCode * 10 + (abiCode != null ? abiCode : 0)
        }
    }

    packagingOptions {
        exclude 'META-INF/versions/9/OSGI-INF/MANIFEST.MF'
        exclude 'META-INF/versions/9/OSGI-INF/bcprov.provider.bc.Licenses'
        exclude 'META-INF/versions/9/OSGI-INF/bcpkix.provider.bc.Licenses'
    }

    ndkVersion = flutter.ndkVersion
}

apply from: "signing.gradle"
apply from: "collect_licenses.gradle"

flutter {
    source = '../..'
}

spotless {
    kotlin {
        target("**/*.kt")
        targetExclude("**/generated/*.kt")
        targetExclude("src/**/ndef/**KeyboardLayout.kt")
        ktlint().editorConfigOverride([ktlint_code_style: "android_studio"])
    }

    kotlinGradle {
        target("*.gradle.kts")
        ktlint()
    }
}

dependencies {
    api "com.yubico.yubikit:android:$project.yubiKitVersion"
    api "com.yubico.yubikit:core:$project.yubiKitVersion"
    api "com.yubico.yubikit:management:$project.yubiKitVersion"
    api "com.yubico.yubikit:oath:$project.yubiKitVersion"
    api "com.yubico.yubikit:fido:$project.yubiKitVersion"
    api "com.yubico.yubikit:piv:$project.yubiKitVersion"
    api "com.yubico.yubikit:support:$project.yubiKitVersion"

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'

    implementation "androidx.core:core-ktx:1.17.0"
    implementation 'androidx.fragment:fragment-ktx:1.8.9'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    implementation 'com.google.android.material:material:1.13.0'

    implementation 'com.github.tony19:logback-android:3.0.0'

    implementation 'org.bouncycastle:bcprov-jdk18on:1.83'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.83'

    // testing dependencies
    testImplementation "junit:junit:$project.junitVersion"
    testImplementation "org.mockito:mockito-core:$project.mockitoVersion"
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
}

// disable OssLicensesTask during testing
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(":app:test") || taskGraph.allTasks.any { it.name.contains("Test") }) {
        tasks.matching { it.name.contains("OssLicensesTask") }.configureEach { task ->
            task.enabled = false
        }
    }
}